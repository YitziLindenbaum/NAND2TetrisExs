// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Screen.jack

/**
 * A library of functions for displaying graphics on the screen.
 * The Hack physical screen consists of 512 rows (indexed 0..511, top to bottom)
 * of 256 pixels each (indexed 0..255, left to right). The top left pixel on
 * the screen is indexed (0,0).
 */
class Screen {
    static boolean current_color;
    static Array twoToThe, screen;

    /** Initializes the Screen. */
    function void init() {
        let current_color = true;
        let screen = 16384;
        let twoToThe = Array.new(16);
        let twoToThe[0] = 1;
        let twoToThe[1] = 2;
        let twoToThe[2] = 4;
        let twoToThe[3] = 8;
        let twoToThe[4] = 16;
        let twoToThe[5] = 32;
        let twoToThe[6] = 64;
        let twoToThe[7] = 128;
        let twoToThe[8] = 256;
        let twoToThe[9] = 512;
        let twoToThe[10] = 1024;
        let twoToThe[11] = 2048;
        let twoToThe[12] = 4096;
        let twoToThe[13] = 8192;
        let twoToThe[14] = 16384;
        let twoToThe[15] = 16384 + 16384;
        return;
    }

    function int multiplyBy32(int x) {
        var int shiftedX, j;
        let j = 0;
        let shiftedX = x;
        while (j < 5) {
            let shiftedX = shiftedX + shiftedX;
            let j = j + 1;
        }
        return shiftedX;
    }

    /** Erases the entire screen. */
    function void clearScreen() {
        var int i, j, address;
        var boolean temp_color;
        let i = 0;
        let temp_color = current_color;
        let current_color = false;
        let address = 16384;
        while (i < 8192) {
            let screen[address + j] = 0;
            let address = address + 32;
            let i = i + 1;
        }
        let current_color = temp_color;
        return;
    }

    /** Sets the current color, to be used for all subsequent drawXXX commands.
     *  Black is represented by true, white by false. */
    function void setColor(boolean b) {
        let current_color = b;
        return;
    }

    /** Draws the (x,y) pixel, using the current color. */
    function void drawPixel(int x, int y) {
        var int address, bit_loc, quotient, mask;
        let quotient = x / 16;
        let bit_loc = x & 15; // x % 16 == x & 0..01111
        let address = Screen.multiplyBy32(y) + quotient;
        if (current_color) { // Current Color is Black, pixel should be 1
            let screen[address] = screen[address] | twoToThe[bit_loc];
        }
        else { // Current Color is White, pixel should be 0
            let screen[address] = screen[address] & ~twoToThe[bit_loc];
        }
        return;
    }

    /** Draws a horizontal line in an optimized manner */
    function void drawHorizontalLine(int x1, int x2, int y) {
        var int bound, left, right, quotient, left_mod, yTimes32, i, address, mask;
        let yTimes32 = Screen.multiplyBy32(y);

        if (x1 = x2) {
            do Screen.drawPixel(x1, y);
            return;
        }
        else {
            /* Determine which x point is on the left and which is on the right */
            let left = Math.min(x1, x2);
            let bound = Math.max(x1, x2);
            let quotient = left / 16;
            let left_mod = left & 15;
            let address = yTimes32 + quotient;
            if (left_mod > 0) {
                if (current_color) { // Current Color is Black, pixel should be 1
                    let i = left_mod;
                    while ((i < 16) & ((left + i - left_mod) < (bound + 1))) { // TODO
                        let screen[address] = screen[address] | twoToThe[i];
                        let i = i + 1;
                    }
                }
                else { // Current Color is White, pixel should be 0; AND(pixel_content, XOR(2^bit_loc, 1111..1))
                    let mask = twoToThe[left_mod] - 1;
                    let screen[address] = screen[address] & mask;
                }
                let left = left + (16 - left_mod);
            }

            /* All of the next 16 pixels appear in the same word and need to be colored */
            while ((left + 16) < bound) {
                let address = yTimes32 + (left / 16);
                if (current_color) { // Current Color is Black
                    let screen[address] = screen[address] | -1;
                }
                else { // Current Color is White
                    let screen[address] = 0;
                }

                let left = left + 16;
            }

            /* Between 1-15 pixels in the same word need to be colored */
            while (left < bound) {
                    do Screen.drawPixel(left, y);
                    let left = left + 1;
                }
        }
        return;
    }

    /** Draws a line from pixel (x1,y1) to pixel (x2,y2), using the current color. */
    function void drawLine(int x1, int y1, int x2, int y2) {
        if (y1 = y2) {
            do Screen.drawHorizontalLine(x1, x2, y1);
        }
        else {
            if (x1 < x2) {
                do Screen.drawLineHelper(x1, y1, x2 - x1, y2 - y1);
            }
            else {
                do Screen.drawLineHelper(x2, y2, x1 - x2, y1 - y2);
            }
        }
        return;
    }

    /** Draws a line according to the algo in figure 12.8 in the book */
    function void drawLineHelper(int x, int y, int dx, int dy) {
        var int a, b, adyMinusbdx;
        var boolean neg_flag;
        let neg_flag = false;
        let a = 0;
        let b = 0;
        let adyMinusbdx = 0;
        if (dy < 0) {
            let neg_flag = true;
            let dy = Math.abs(dy);
        }
        while (((a < dx) | (a = dx)) & ((b < dy) | (b = dy))) {
            if (neg_flag) {
                do Screen.drawPixel(x + a, y - b);
            }
            else {
                do Screen.drawPixel(x + a, y + b);
            }
            if (adyMinusbdx < 0) {
                let a = a + 1;
                let adyMinusbdx = adyMinusbdx + dy;
            }
            else {
                let b = b + 1;
                let adyMinusbdx = adyMinusbdx - dx;
            }
        }
        return;
    }

    /** Draws a filled rectangle whose top left corner is (x1, y1)
     * and bottom right corner is (x2,y2), using the current color. */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
        var int i;
        let i = y1;
        while (i < y2) {
            do Screen.drawHorizontalLine(x1, x2, i);
            let i = i + 1;
        }
        return;
    }

    /** Draws a filled circle of radius r<=181 around (x,y), using the current color. */
    function void drawCircle(int x, int y, int r) {
        var int dy, rad_thing, rSquared;
        if ((r < 1) | (r > 181)) {
            do Sys.error(1);
        }
        else {
            let dy = -r;
            let rSquared = r * r;
            while (dy < (r + 1)) {
                let rad_thing = Math.sqrt(rSquared - (dy * dy));
                do Screen.drawHorizontalLine(x - rad_thing, x + rad_thing, y + dy);
                let dy = dy + 1;
            }
        }
        return;
    }
}
